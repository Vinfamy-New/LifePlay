WHAT: all
WHERE: nightclub, bar
WHEN: 0 - 24
WHO: Actor2 = getCompanion(); If Actor2:rapportwithplayer > 20 && Actor2.isSameGender(Player) && !Actor2.isDating() && [Actor2:age - age < 10 || age - Actor2:age < 10] && !Player.isAsexual()
OTHER:

    // While hanging out with a friend of the same gender and similar age at a nightclub / bar, you may get approached by a pair of older (wo)men. This, depending your own and your friend's likes_older, perversion, interpersonal stats and scene choices, may result in either direct or voyeur partipation in a threesome or foursome.
    
    SceneStart()
    Actor2.moveToPersonStand(Player, 100)
    "I was hanging out with <Actor2.name> at the bar when a pair of strangers approached us."
    Actor = generatePersonTemporary()
    While !Player.isInterestedIn(Actor)
        Actor = generatePersonTemporary()
    Endwhile
    Actor.makeInterested(Player)
    
    Actor:age => age + likes_older*0.5
    Actor:age += Random(-5, 5)
    Actor:attractiveness => Random(70, 100)
    Actor:interpersonal => Random(70, 100)
    Actor.randomizeFace()
    Actor.randomizeHairs()
    Actor:likes_older => 2*[age - Actor:age]
    Actor:attractiontoplayer += Random(25, 50)
    
    Actor.dress()
    Actor.show()
    Actor.moveToPersonStand(Player, 100)

    Actor3 = generatePersonTemporary()
    While !Actor.isSameGender(Actor3)
        Actor3 = generatePersonTemporary()
    Endwhile
    Actor3.makeInterested(Player)
    
    Actor3:age => age + likes_older*0.5
    Actor3:age += Random(-5, 5)
    Actor3:attractiveness => Random(70, 100)
    Actor3:interpersonal => Random(70, 100)
    Actor3.randomizeFace()
    Actor3.randomizeHairs()
    Actor3:likes_older => 2*[age - Actor3:age]
    Actor3:attractiontoplayer += Random(25, 50)
    
    Actor3.dress()
    Actor3.show()
    Actor3.moveToPersonStand(Player, 100)
    
    "The strangers introduced themselves as <Actor.name> and <Actor3.name> and were rather confident about telling me and my friend that they find us attractive and offering to buy us drinks ..."
    
    If Random(0, 40) < Actor2:likes_older
        FriendApproves = true
        "Judging from <Actor2.name>'s initial reaction, <Actor2.he_or_she> seems rather interested in our new guests, especially <Actor3.name>. I didn't know <Actor2.he_or_she> was into older <Actor3.men_or_women>."
    Else
        FriendApproves = false
        "Judging from <Actor2.name>'s initial reaction, <Actor2.he_or_she> seems uncomfortable with the whole situation. <Actor2.He_or_She> didn't seem to be into older <Actor3.men_or_women> and probably just thought of them as a pair of creepy old <Actor3.men_or_women> trying to score with young <Player.guys_or_girls> at a social establishment they're far too old for."    
    Endif
    "More importantly though, what do I think of these older <Actor3.men_or_women>?"
    0:: "Accept drinks and hang out with them"
    1:: "Reject them"
    
    If 0
        If FriendApproves
            Ahead = true
            "It seems that <Actor2.name> and I have similar taste in <Actor3.men_or_women> ..."
        Else
            "<Actor2.name> seemed a bit shocked by how receptive I was to <Actor.name> and <Actor3.name>"
            If Random(0, 100) < interpersonal
                "Fortunately though, I managed to whisper into <Actor2.his_or_her> ear and convince <Actor2.him_or_her> to give them a chance ..."
                Ahead = true
                Actor2:likes_older += 5
            Else
                "And <Actor2.he_or_she> rather rudely got rid of the pair, on our behalf. I never had a chance to convince <Actor2.him_or_her> otherwise."
                Ahead = false
            Endif
        Endif
    Else
        If FriendApproves
            "<Actor2.name> seemed disappointed that I was about to chase away someone <Actor2.he_or_she> would be interested in"
            If Random(0, 100) < Actor2:interpersonal
                "In the end, <Actor2.he_or_she> managed to guilt-trip me in giving <Actor.name> and <Actor3.name> a chance"
                Ahead = true
                likes_older += 5
            Else
                "I ignored <Actor2.his_or_her> objection and rather rudely got rid of the pair, on our behalf. <Actor2.He_or_She> never had a chance to convince me otherwise."
                Ahead = false
            Endif
        Else
            Ahead = false
            "It seems that <Actor2.name> and I share a similar distaste in <Actor3.men_or_women> like these ..."
        Endif
    Endif
    
    If Ahead
        Player.animate(drinkglass)
        Actor.animate(drinkglass)
        Actor2.animate(drinkglass)
        Actor3.animate(drinkglass)
        "<Actor.name> and <Actor3.name> bought us rounds after rounds of hard alcohol. They asked for our numbers and we couldn't really say no."
        Actor.makePermanent()
        Actor3.makePermanent()
        exchangeContact(Actor)
        exchangeContact(Actor3)
        Player.animate(dance)
        Actor.animate(dance)
        Actor2.animate(dance)
        Actor3.animate(dance)
        "And we chat and danced our night away, having a rather good time with our new and older friends, whatever reservations we had beforehand. The alcohol definitely helped."
        Player.animate()
        Actor.animate()
        Actor2.animate()
        Actor3.animate()        
        "As the bar called for the last order, <Actor.name> and <Actor3.name> invited us to move to party to their apartment. It's no secret how they want the night to end ..."
        0:: "Go to their apartment"
        1:: "Reject them"
        
        If 0
            perversion += 3
            If FriendApproves
                "<Actor2.name> also seems keen to go back to their apartment for an 'after-party' so we decided to follow our new friends home."
                setBackground(home)
                "And we might be younger than <Actor.name> and <Actor3.name>, but we're not innocent children. We knew exactly what would happen after following them home."
                "A night out with my friend is about to get a lot more 'mature' in multiple meanings"
                Sex(Player, Actor)
                Sex(Actor2, Actor3)
                Sex(Actor3, Actor, Player, Actor2)
            Else
                "<Actor2.name> seemed shocked that I was willing to go all the way with these two older <Actor3.men_or_women> we literally just met a hour ago."
                "Even though I managed to convince <Actor2.name> not to be rude and give them a chance earlier, this clearly crossed the limit for what <Actor2.he_or_she> is willing to do, and <Actor2.he_or_she> excused <Actor2.himself_or_herself> while wishing me a good night in a rather judgmental tone."
                Actor2.hide()
                endDate()
                setBackground(home)
                "And I might be younger than <Actor.name> and <Actor3.name>, but I'm not an innocent child. I knew exactly what would happen after following them home."
                "And since my friend bailed on me, I have both of them to myself."
                Sex(Actor, Player)
                Sex(Actor3, Player)
                Sex(Actor, Actor3, Player)
            Endif        
        Else
            If FriendApproves
                "Unlike me, <Actor2.name> seems keen to go back to their apartment for an 'after-party' so we parted ways."
                Player.hide()
                setBackground(home)
                "And <Actor2.name> might be younger than <Actor.name> and <Actor3.name>, but <Actor2.he_or_she> is not an innocent child. <Actor2.He_or_She> knew exactly what would happen after following them home."
                "And since I bailed on <Actor2.him_or_her>, <Actor2.name> have both of them to <Actor2.himself_or_herself>."
                Sex(Actor, Actor2)
                Sex(Actor3, Actor2)
                Sex(Actor, Actor3, Actor2)
                
                Actor.hide()
                Actor2.hide()
                Actor3.hide()
                endDate()
                setBackground(street)
                Player.show()
                "I can only imagine what kind of things the two older <Actor3.men_or_women> are doing to my friend right now ... Not sure if I should feel relieved or jealous ..."
            Else
                "Neither of us were interested in the idea of going that far having just met them ..."
                "So they left us alone, thankfully. Still, they have our numbers now, so I imagine they will try their luck again."
            Endif
        Endif
    Endif
    SceneEnd()
    timeout(200, oldyoung_double_date)