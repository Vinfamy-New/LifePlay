WHAT: all, -go_to_the_bathroom
WHERE: all, -toilets, -PC, -office
WHEN: 0-24
WHO: Companion = getCompanion(); If !CurrentCompanion.isDating() && [CurrentCompanion.hasRelationship(Poly) || !CurrentCompanion.hasRelationship(Spouses, Dating)] && CurrentCompanion.IsInterestedIn(Player) && Player.IsInterestedIn(CurrentCompanion) && CurrentCompanion:attractiontoplayer > Random(10, 50) && CurrentCompanion:perversion > 50
OTHER: Player.isDating() && OpenRelationship()

    SceneStart()
    CurrentCompanion(Anxious):: "Listen, can we talk privately for a bit?"
    "<CurrentCompanion.name> has been acting rather weirdly today, as if <CurrentCompanion.he_or_she> had something important to say to me all day."
    Player(Curious):: "Of course, <CurrentCompanion.name>. What is it?"
    CurrentCompanion(Happy):: "You know, although we haven't known each other for too long, I have grown quite fond of you and I hope you feel the same ..."
    CurrentCompanion(Happy):: "If so, I would love nothing more than to take our relationship to the next level and be able to call you my <Player.boyfriend_or_girlfriend>."
    CurrentCompanion(Happy):: "I know you're curently in a polyamorous relationship, so you guys wouldn't mind if I ... join, would you?"    
    0:: "Take <CurrentCompanion.name> on as another polyamorous lover"
    1:: "Reject <CurrentCompanion.him_or_her>"
    
    If 0
        Player(Happy):: "I like you a lot too, <CurrentCompanion.name>. Of course I'll be your <Player.boyfriend_or_girlfriend>! I'm sure the others would love to 'personally' welcome you into our special relationship."
        mood += 100
        0:: "Take <CurrentCompanion.name> to 'initiate' <CurrentCompanion.him_or_her> with my other lovers"
        1:: "Continue our date"
        
        If 0
            Player(Wink):: "Come with me, so that I can introduce you to my other lovers as the latest member of our special family!"
            moveTo(home)
            setBackground(home)
            Actor = Player
            While Actor.isValid()
                Actor = getSpecific(Dating)
                If Actor.isValid()
                    Actor.dress()
                    Actor.show()
                    Actor:: "Welcome, <CurrentCompanion.name>! Let me show you how it works in this relationship."
                    Sex(Player, Actor, CurrentCompanion)
                Endif
            Endwhile
            "After having sex, we continued our date, with <CurrentCompanion.name> as my latest lover."
            CurrentCompanion.setDating()
        Else
            "We continued our date, with <CurrentCompanion.name> as my latest lover."
            CurrentCompanion.setDating()
        Endif
        exchangeContact(CurrentCompanion)
    Else
        Player(Surprised):: "<CurrentCompanion.name> ... I like you too ... but just not that way. I only see you as a friend ..."
        mood -= 10
        "It was a difficult conversation to have. On one hand, I don't have any romantic interest in <CurrentCompanion.name>. On another hand, I don't want to lose <CurrentCompanion.him_or_her> as a friend." 
        "Eventually ..."
        If CurrentCompanion:rapportwithplayer + interpersonal > Random(0, 50)
            CurrentCompanion(Sad):: "Alright, I respect your decision. Let's just be friends ..."
            CurrentCompanion:attractiontoplayer -= Random(0, 20)
            "Those words were clearly said with a lot of reluctance, but at least I managed to keep <CurrentCompanion.name> in my life ... for now."
        Else
            CurrentCompanion(Offended):: "No, if I cannot have your heart, then I'd rather not have you in my life at all!"
            "Having said that, <CurrentCompanion.name> stormed off. Was that the last I've seen of <CurrentCompanion.him_or_her>?"
            CurrentCompanion.hide()
            setBackground(home)
            "A few hours later ..."
            passTime(1, 2)
            "Thinking that <CurrentCompanion.he_or_she> would have calmed down by now, I tried to call, only to find out that I had been blocked! It's a sad day, I might have forever lost <CurrentCompanion.name> from my life ..."
            blockContact(CurrentCompanion)
            endDate()                
        Endif
    Endif

    SceneEnd()
    timeout(300, npc_joins_poly)