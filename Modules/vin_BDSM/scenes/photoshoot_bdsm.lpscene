WHAT: all
WHERE: all, -work
WHEN: 8 - 22
WHO: none
OTHER: Random(1, 150) < modelfame && Random(30, 100) < attractiveness && !Player.isMale()

    SceneStart()
    
    "A fetish fashion brand wants to book me to model for one of their new BDSM collections ..."
    0:: "Turn down the offer"
    1:: "Rip them off"
    2:: "Offer a fair price"
    3:: "Give them a bargain"
    
    If 0
        Player(Sad):: "Sorry, but I have a holiday planned on the day of the shoot ..."
    Elseif 3 || Random(0, 100) < choice*modelfame
        "The company accepted my demands."
        Player.strip()
        setbackground(modeling)
        "The day of the shoot ..."
        Actor = generatePersonTemporary()
        Player.dressBondage(Collar)
        Actor.show(2)
        Actor(Happy):: "Here, put this on."
        Player.dressBondage(Handcuffs)
        Actor(Happy):: "That's it ... perfect."
        Player.dressBondage(Gag)
        Actor(Happy):: "Put more emotions into it ... there you go."
        Player.dressBondage(Torso)
        Actor(Happy):: "A bit more to the left ... that's perfect."
        Player.dressBondage()
        Actor(Happy):: "Yup, that's how you do it. This is going to look so great!"
        
        money += Random(400, 1500)/choice
        modelfame += 0.25
        
        If Actor.isInterestedIn(Player) && Actor:perversion > 50
            "Throughout the shoot today, I can't help but notice how flirty the photographer has been."
            Actor(Flirty):: "You know, to get far in this business, only look is not enough. We have to have a certain 'friendly' attitude."
            AnimatePair(Player, Actor, Kissing)
            Actor(Kiss):: "Mwah"
            Actor(Flirty):: "You have great skin ... Oh look, where's my manner? I just couldn't help myself but touching you."
            "Touching is an understatement, this perverted photographer was basically groping me."
            0:: "Shut <Actor.him_or_her> down"
            1:: "Flirt back"
            2:: "Kiss <Actor.him_or_her>"
            
            If 0
                Player(Angry):: "Sorry, I'm not a 'casting couch' type of <Player.guy_or_girl>. Why don't you find yourself some other <Player.gigolo_or_whore>?"
                Actor(Angry):: "No need to get so serious! With that attitude, you're going nowhere!"
                modelfame -= 1
            Elseif 1
                Player(Flirty):: "You're not bad looking yourself. I'm surprised you're always behind the camera because you look like you could be in front of it."
                "I guess some harmless flirting for career advancement is a good deal."
                modelfame += 1
            Else
                Player(Flirty):: "Is this the sort of friendly attitude you're taking about?"
                "What's the harm in a kiss anyway? Especially if it gets me ahead of the competition."
                Filter(NonSexual)
                SexNoAffair(Actor, Player)
                modelfame += 3
            Endif
        Endif
    Else
        "Unfortunately, the company considered my rate too high and hired another model for the job instead."
    Endif
    
    SceneEnd()
    timeout(100, photoshoot_bdsm)