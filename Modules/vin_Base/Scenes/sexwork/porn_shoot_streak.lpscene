WHAT: all
WHERE: all, -work
WHEN: 8 - 18
WHO: none
OTHER: Random(1, 300) < pornfame && !Player.isAsexual()

    SceneStart()
    Dice = Random(1500, 6000)
    P1C = Dice.convertToLocalCurrency(true)
    P2 = Dice/2
    P2C = P2.convertToLocalCurrency(true)
    P3 = Dice/3
    P3C = P3.convertToLocalCurrency(true)
    "A porn director wants to book me for a scene where I walk around naked in public and have sex with any strangers who become interested."    
    "This is rather risky both in terms of STDs and in terms of getting arrested for public indecency ..."
    0:: "Turn down the offer"
    1:: "Rip the director off (<P1C>)"
    2:: "Offer a fair price (<P2C>)"
    3:: "Give the director a bargain (<P3C>)"
    
    If 0
        Player(Sad):: "Sorry, but I have a holiday planned on the day of the shoot ..."
    Elseif 3 || Random(0, 100) < choice*pornfame
        "The porn director accepted my demands and soon enough we were meeting at the shoot location that we agreed on."
        Random
            setBackground(bar)
            setBackground(hairdresser)
            setBackground(cafe)
            setBackground(clothes)
            setBackground(dentist)
            setBackground(doctor)
            setBackground(hotel)
            setBackground(laundry)
            setBackground(marketplace)
            setBackground(musical_instrument)
            setBackground(nightclub)
            setBackground(work)
            setBackground(place_of_worship)
            setBackground(police)
            setBackground(pub)
            setBackground(restaurant)
            setBackground(shoes)
            setBackground(sports_centre)
            setBackground(supermarket)
            setBackground(theatre)
        endRandom
        Player.strip()
        "I removed all my clothes, to the shock of all members of the public there, except the porn crew. Soon though, I can tell some of the more perverted strangers start processing what's going on and considering whether they should participate."
        money => money + Dice/choice
        pornfame += 2
        perversion += 1
        count = 0
        While count < 7
            count += 1
            Actor = generatePersonTemporary()
            If Player.isInterestedIn(Actor)
                Actor.dress()
                Actor.show()
                "No longer able to hold back <Actor.his_or_her> arousal, a stranger approaches me ..."
                Sex(Actor, Player)
            Endif
            Actor.hide()
        Endwhile
        "That's all for the shoot. That was an intense experience, and a very well-paid one at that ..."
    Else
        "No way, that's way above the market rate for this type of shoots! You have too high an opinion of yourself, honey. You should ask the other pornstars how much they charge."
        0:: "Say nothing"
        1:: "Cheap motherfucker!"
        
        If 1
            If Random(0, 100) < 30
                "The director got pissed off and tried to hit me"
                If Random(0, 100) < martial
                    "Unfortunately for him, I was no pushover and gave him a good spanking."
                    "As things would have it, our little incident went viral in the porn industry media and somehow made me even more popular. I guess no publicity is bad publicity. Besides, other directors will have to think twice now before trying to bully me in the future."
                    pornfame += 2
                Else
                    "I tried to stand my ground but ..."
                    "Unfortunately for me, he was a powerful man and I got quite seriously hurt."
                    "As things would have it, our little incident went viral in the porn industry media and somehow made me a butt of jokes. No doubt this would affect my future prospects with other directors."
                    pornfame -= 2                
                Endif
            Elseif Random(0, 100) < 50
                "How dare you? You'll never work for this studio again!"
                pornfame -= 1
            Else
                "Fine, I changed my mind. Let's try negotiating again ..."
                followUp(porn_shoot_streak)
            Endif
        Endif
    Endif
    
    SceneEnd()
    timeout(500, porn_shoot_streak)