WHAT: all
WHERE: home
WHEN: 0 - 24
WHO: Dating = getSpecific(Dating)
OTHER: !isWithCompanion() && masochist + perversion > Random(0, 150) && isModEnabled(vin_NTR)

    SceneStart()
    "I love <Dating.name> very much, but have I ever fantasized about <Dating.him_or_her> being with another <Player.man_or_woman> of a different race?"
    0:: "Yes"
    1:: "No"

    If 0
        "Which race should <Dating.name>'s lover be?"
        0:: !Dating.isEthnic(black) && !Player.isEthnic(black):: "Black"
        1:: !Dating.isEthnic(indian) && !Player.isEthnic(indian):: "South Asian"
        2:: !Dating.isEthnic(arab) && !Player.isEthnic(arab):: "Arab"
        3:: !Dating.isEthnic(latin) && !Player.isEthnic(latin):: "Latin"
        4:: !Dating.isEthnic(asian) && !Player.isEthnic(asian):: "East Asian"
        5:: !Dating.isEthnic(white) && !Player.isEthnic(white):: "White"
        
        If 0
            setRaceGenerated(black)
        Elseif 1
            setRaceGenerated(indian)    
        Elseif 2
            setRaceGenerated(arab)        
        Elseif 3
            setRaceGenerated(latin)    
        Elseif 4
            setRaceGenerated(asian)    
        Else
            setRaceGenerated(white)    
        Endif
        
        Actor = generatePersonTemporary()
        While !Player.isSameGender(Actor)
            Actor = generatePersonTemporary()
        Endwhile
        
        "I must admit: the thought of watching <Dating.name> have sex with a <Actor.race> <Actor.man_or_woman> really turns me on."
        
        If Actor.isEthnic(black) && Actor.isMale() && !isTimingOut(strt_black_m)
            Actor.blendPreset(strt_black_m)
            "I can't help but fantasize about watching <Dating.name>'s poor little <Dating.race> body being stretched to its limit by a big black cock."
            
        Elseif Actor.isEthnic(black) && !Actor.isMale() && !isTimingOut(strt_black_f)
            Actor.blendPreset(strt_black_f)
            "I can't help but fantasize about watching <Dating.name> with a strong black woman and her voluptuous curves."


        Elseif Actor.isEthnic(indian) && Actor.isMale() && !isTimingOut(strt_indian_m)
            Actor.blendPreset(strt_indian_m)
            "I can't help but fantasize about watching <Dating.name> being pounded by an exotic South Asian man with his dark skin matched by thick black body hair."
            
        Elseif Actor.isEthnic(indian) && !Actor.isMale() && !isTimingOut(strt_indian_f)
            Actor.blendPreset(strt_indian_f)
            "I'd love to watch <Dating.name> open up an exotic yet sexually repressed South Asian woman, especially if she's a bit on the hairier side."

            
        Elseif Actor.isEthnic(arab) && Actor.isMale() && !isTimingOut(strt_arab_m)
            Actor.blendPreset(strt_arab_m)
            "I can't help but fantasize about watching <Dating.name> being fucked by an exotic Middle Eastern man with his manly beard and hairy chest."
            If Dating.isMale()
                "The fact that homosexuality is a forbidden fruit in his culture makes it even more exciting."
            Else
                "I want to watch first-hand how he would treat my woman with a more dominant attitude."
            Endif
            
        Elseif Actor.isEthnic(arab) && !Actor.isMale() && !isTimingOut(strt_arab_f)
            Actor.blendPreset(strt_arab_f)
            "I'd love to watch <Dating.name> open up an exotic yet sexually repressed Middle Eastern woman."
            If !Dating.isMale()
                "The fact that homosexuality is a forbidden fruit in her culture makes it even more exciting."
            Endif


        Elseif Actor.isEthnic(latin) && Actor.isMale() && !isTimingOut(strt_latin_m)
            Actor.blendPreset(strt_latin_m)
            "I can't help but fantasize about watching <Dating.name> making passionate love with a dark and handsome Latino man, who could bring <Dating.him_or_her> to orgasm just with his sexy voice."
            
        Elseif Actor.isEthnic(latin) && !Actor.isMale() && !isTimingOut(strt_latin_f)
            Actor.blendPreset(strt_latin_f)
            "I can't help but fantasize about watching <Dating.name> dance all night in bed with a sexy Latina with her voluptuous curves and fiery passion."

            
        Elseif Actor.isEthnic(asian) && Actor.isMale() && !isTimingOut(strt_asian_m)
            Actor.blendPreset(strt_asian_m)
            "I can't help but fantasize about watching <Dating.name> making love with a cute and gentle East Asian guy. I know <Dating.he_or_she> would enjoy being with someone intelligent."
            
        Elseif Actor.isEthnic(asian) && !Actor.isMale() && !isTimingOut(strt_asian_f)
            Actor.blendPreset(strt_asian_f)
            "I'd love to watch <Dating.name> open up an smart yet shy & submissive East Asian woman. I'm curious to see whether <Dating.name> would be willing to eat the pussy of a lady who's all natural down there."


        Elseif Actor.isEthnic(white) && Actor.isMale() && !isTimingOut(strt_white_m)
            Actor.blendPreset(strt_white_m)
            "I can't help but fantasize about <Dating.name> being fucked by a white man. Subconsciously, maybe I enjoy a little bit being belittled by someone with a more dominant perceived social status and whose race historically was in a position of authority over my own <Player.race> people. Besides, I get turned on just thinking about the contrast between his pale skin and <Dating.name>'s <Dating.race> tone when they make love."
            
        Elseif Actor.isEthnic(white) && !Actor.isMale() && !isTimingOut(strt_white_f)
            Actor.blendPreset(strt_white_f)
            "I can't help but fantasize about <Dating.name> being with a white woman. Subconsciously, maybe I enjoy a little bit feel inferior to someone whose race historically was in a position of authority over my own <Player.race> people and even now still considered by some as the standard for beauty. Besides, I get turned on just thinking about the contrast between her pale skin and <Dating.name>'s <Dating.race> tone when they make love."
        Endif
    
        masochist += Random(0, 1)
        perversion += Random(0, 0.5)
        "But am I ready to do something about it?"
        0:: "Discuss my fantasy with <Dating.name>"
        1:: "No way"
        
        If 0
            "Yes, I should probably talk to <Dating.name> about it."
            "That night ..."
            Dating.dress()
            Sex(Player, Dating)
            Dating.show(2)
            Dating(Flirty):: "Baby, that was such pleasure you gave me."
            Player(Flirty):: "Love, I have something to confess to you ..."
            "I took advantage of our pillow talk to share with <Dating.name> my deepest fantasy of being cuckolded by an <Actor.race> <Actor.man_or_woman>..."
            If Random(0, 50) < Dating:perversion
                Dating(Flirty):: "You naughty <Player.boy_or_girl>! Fancy yourself as more of a voyeur, don't you? Fine then, if that's what turns you on, I can give it a try. Just make sure to find me a hot <Actor.race> <Player.boy_or_girl> to fuck, okay?"
                "We then went on to discuss potential candidates."
                secondScreen(Actor)
                Actor.show()
                "<Dating.name> then admitted that <Dating.he_or_she> had a <Actor.race> friend called <Actor.name> who <Dating.he_or_she> has always found attractive. This would be <Dating.his_or_her> candidate."
                0:: "Agree to <Dating.name>'s wish"
                1:: "Insist on picking the candidate myself"
                
                If 0
                    secondScreen()
                    Actor.makePermanent()
                    GoAhead = true
                Else
                    secondScreen()
                    chooseContact()
                    Actor2 = getSpecific(chooseContact)
                    While !Actor2.isSameEthnic(Actor)
                        "<Actor2.name> isn't <Actor.race>, which is the whole point of my fantasy."
                        chooseContact()
                        Actor2 = getSpecific(chooseContact)
                    Endwhile
                    
                    If Actor2.isValid()
                        "After a while, <Dating.name> admits that <Dating.he_or_she> finds <Actor2.name>, one of my <Actor2.race> friends, attractive."
                        Actor = Actor2
                        GoAhead = true
                    Else
                        "Unfortunately, we couldn't quite agree on which <Actor.race> <Actor.guy_or_girl> <Dating.name> would want to fuck. Oh well, at least now I know <Dating.he_or_she> is interested in fulfilling this fantasy of mine."
                        GoAhead = false
                    Endif
                Endif
                
                If GoAhead
                    "With our plan in motion, I approached and managed to convince <Actor.name> to participate in our little perverted adventure."
                    "The all important day is here ..."
                    Actor.dress()
                    Actor.show(3)
                    "<Dating.name>, <Actor.name> and I had dinner at my apartment, before <Dating.name> took <Dating.his_or_her> new <Actor.race> lover's hand and led <Actor.him_or_her> into the bedroom - the same one that we just had sex in a few nights ago."
                    "It's finally time for my <Dating.race> <Dating.boyfriend_or_girlfriend> to experience intimacy with a <Actor.race> <Actor.man_or_woman>, and finally time for me to watch it happen."
                    Player.hide()
                    Sex(Actor, Dating)
                    masochist += Random(0, 2)
                    perversion += Random(0, 1)
                    "That was hot. But it looks like both <Actor.name> and <Dating.name> could go for another round ... and I'm becoming quite aroused."
                    0:: "Have a threesome with our new <Actor.race> friend"
                    1:: "Nope"
                    
                    If 0
                       Sex(Actor, Dating, Player)
                       perversion += Random(0, 1)
                    Endif
                    Actor.hide()
                    Dating.show(2)
                    Player.show(0)
                    "After the sex, <Actor.name> got up and left without saying a word, as agreed beforehand. That was a sensual experience like no other to watch my <Dating.boyfriend_or_girlfriend> being <Dating.pleasured_or_defiled> by a <Actor.race> <Actor.man_or_woman>."
                    Dating:attractiontoplayer += Random(0, 2)
                    timeout(200, cuckold_ir)
                Endif
            Else
                Dating(Angry):: "What? You want me to sleep with another <Player.man_or_woman>. That's disgusting. How could you even consider it? Do you not love me anymore?"
                Dating:attractiontoplayer -= Random(0, 2)
                "There's no point pursuing this anymore. There's no way I can get <Dating.name> to agree to this."
                timeout(200, cuckold_ir)
            Endif 
        Else
            "Sometimes, fantasies should just remain fantasies ..."
        Endif
    Else
        "No way. That thought breaks my heart. I would never be cool with that."
    Endif

    timeout(100, cuckold_ir)
    SceneEnd()